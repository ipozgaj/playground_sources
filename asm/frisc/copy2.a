; U memoriji pocevsi od adrese 40h nalazi se blok podataka u 32 bitnom formatu 
; dvojnog komplementa. Velicina bloka nije unaprijed zadana, ali se zna da je 
; blok zakljucen podatkom 80000000h. Treba napisati program za FRISC koji ce 
; podatke iz opisanog bloka kopirati u novi blok pocevsi od adrese 50h, ali 
; tako da podaci u njemu budu u 64 bitnom formatu sa bitom za predznak (tj. 
; format sa bitom za predznak u dvostrukoj preciznosti). Nizi dio 64 bitnog 
; broja treba biti na nizoj memorijskoj adresi, a visi dio na na visoj adresi. 
; Podaci u pocetnom bloku su: -1, +3, -4, +48, -96 (dekadski brojevi)
;
; (C) 2004  Igor Pozgaj <ipozgaj@fly.srk.fer.hr>


	`ORG	0

	LOADL	R1, 40			; izvor
	LOADL	R2, 50			; odrediste
	LOADL	R4, #LO 80000000	; podatak za oznaku kraja bloka
	LOADH	R4, #HI 80000000

NEXT	LOAD	R0, (R1)
	CMP	R0, R4			; gotovi?
	JR_NE	.+2
	HALT
	CLEAR	R3			; R3 sadrzi gornja 32 bita u 64 bitnom prikazu s
	TEST	R0
	JP_P	SAVE			; broj pozitivan?

	NEG	R0			; negativan broj (dvojno komplementiraj)
	LOADH	R3, #HI 80000000	; dodaj predznak u gornjih 32 bita

SAVE	STORE	R0, (R2)		; spremi donji dio
	STORE	R3, (R2+1)		; spremi gornji dio
	INC	R1			; prijedji na iduci podatak
	ADD	2, R2, R2		; na odredite upisujemo 64 bita!
	JP	NEXT
	
	`ORG	40
	`DW	%D-1, %D+3, %D-4, %D+48, %D-96, 80000000, 1, 2, 3, 4

